## Natural Language Toolkit: feat0.fcfg
##
## First example of a feature-based grammar for English, illustrating
## value-sharing of NUM and TENSE features.
## Used in Feature-Based Grammars chapter.
## 
## Author: Ewan Klein <ewan@inf.ed.ac.uk> 
## URL: <http://nltk.sourceforge.net>
## For license information, see LICENSE.TXT
##
## $Id: feat0.cfg 4674 2007-06-14 08:55:32Z ehk $

% start S
# ###################
# Grammar Productions
# ###################

# S expansion productions
S -> NP[NUM=?n] VP[NUM=?n]

# NP expansion productions 
NP[NUM=?n] -> PropN[NUM=?n] 
NP[NUM=pl, COUNT=c] -> Det[NUM=pl] N[NUM=pl, COUNT=c]
NP[COUNT=un] -> Det[NUM=sg] N[COUNT=un]
NP[COUNT=un] -> N[COUNT=un]
NP[NUM=pl, COUNT=c] -> N[NUM=pl, COUNT=c] 
NP[NUM=sg, COUNT=c] -> Det[NUM=sg] N[NUM=sg, COUNT=c]

# VP expansion productions
VP[TENSE=?t, NUM=?n] -> Cop[TENSE=?t, NUM=?n] Adj
VP[TENSE=?t, NUM=?n] -> IV[TENSE=?t, NUM=?n]
VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n] NP

# ###################
# Lexical Productions
# ###################

Det[NUM=sg] -> 'this' | 'every'
Det[NUM=pl] -> 'these' | 'all'
Det -> 'the' | 'some' | 'several' | 'The'

PropN[NUM=sg]-> 'Kim' | 'Jody'

N[NUM=sg, COUNT=c] -> 'dog' | 'girl' | 'car' | 'child' | 'boy' | 'Boy'
N[NUM=pl, COUNT=c] -> 'dogs' | 'girls' | 'cars' | 'children'| 'boys'| 'Boys'

N[COUNT=un] -> 'water' | 'Water'

IV[TENSE=pres,  NUM=sg] -> 'disappears' | 'walks' | 'sings'
TV[TENSE=pres, NUM=sg] -> 'sees' | 'likes'

IV[TENSE=pres,  NUM=pl] -> 'disappear' | 'walk'| 'sing'
TV[TENSE=pres, NUM=pl] -> 'see' | 'like'

IV[TENSE=past] -> 'disappeared' | 'walked'
TV[TENSE=past] -> 'saw' | 'liked'

Cop[TENSE=pres, NUM=sg] -> 'is'
Adj -> 'precious'

